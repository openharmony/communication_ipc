# Copyright (C) 2021-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../config.gni")

SUBSYSTEM_DIR = "//foundation/communication/ipc"
IPC_CORE_ROOT = "$SUBSYSTEM_DIR/ipc/native"

config("libipc_core_private_config") {
  cflags_cc = [ "-O2" ]
}

config("ipc_all_deps_config") {
  include_dirs = [ "$SUBSYSTEM_DIR/interfaces/innerkits/ipc_core/include" ]
  if (is_emulator) {
    defines = [ "EMULATOR_PLATFORM" ]
  }
}

ohos_shared_library("ipc_core") {
  sanitize = {
    integer_overflow = true
  }
  version_script = "libipc_core_map"
  branch_protector_ret = "pac_ret"
  include_dirs = [
    "$IPC_CORE_ROOT/c/adapter/access_token/include",
    "$IPC_CORE_ROOT/c/rpc/include",
    "$SUBSYSTEM_DIR/utils/include",
  ]
  sources = [
    "$IPC_CORE_ROOT/c/adapter/access_token/src/access_token_adapter.c",
    "$IPC_CORE_ROOT/c/rpc/src/rpc_feature_set.c",
    "$IPC_CORE_ROOT/src/core/source/buffer_object.cpp",
    "$IPC_CORE_ROOT/src/core/source/comm_auth_info.cpp",
    "$IPC_CORE_ROOT/src/core/source/databus_socket_listener.cpp",
    "$IPC_CORE_ROOT/src/core/source/dbinder_callback_stub.cpp",
    "$IPC_CORE_ROOT/src/core/source/dbinder_session_object.cpp",
    "$IPC_CORE_ROOT/src/core/source/dbinder_softbus_client.cpp",
    "$IPC_CORE_ROOT/src/core/source/ipc_file_descriptor.cpp",
    "$IPC_CORE_ROOT/src/core/source/ipc_object_proxy.cpp",
    "$IPC_CORE_ROOT/src/core/source/ipc_object_stub.cpp",
    "$IPC_CORE_ROOT/src/core/source/ipc_payload_statistics.cpp",
    "$IPC_CORE_ROOT/src/core/source/ipc_process_skeleton.cpp",
    "$IPC_CORE_ROOT/src/core/source/ipc_skeleton.cpp",
    "$IPC_CORE_ROOT/src/core/source/ipc_thread_pool.cpp",
    "$IPC_CORE_ROOT/src/core/source/ipc_thread_skeleton.cpp",
    "$IPC_CORE_ROOT/src/core/source/ipc_workthread.cpp",
    "$IPC_CORE_ROOT/src/core/source/iremote_broker.cpp",
    "$IPC_CORE_ROOT/src/core/source/iremote_object.cpp",
    "$IPC_CORE_ROOT/src/core/source/message_option.cpp",
    "$IPC_CORE_ROOT/src/core/source/message_parcel.cpp",
    "$IPC_CORE_ROOT/src/core/source/peer_holder.cpp",
    "$IPC_CORE_ROOT/src/core/source/stub_refcount_object.cpp",
    "$IPC_CORE_ROOT/src/mock/source/binder_debug.cpp",
    "$IPC_CORE_ROOT/src/mock/source/binder_invoker.cpp",
    "$IPC_CORE_ROOT/src/mock/source/dbinder_databus_invoker.cpp",
    "$IPC_CORE_ROOT/src/mock/source/hitrace_invoker.cpp",
    "$IPC_CORE_ROOT/src/mock/source/invoker_factory.cpp",
    "$IPC_CORE_ROOT/src/mock/source/invoker_rawdata.cpp",
  ]

  configs = [
    "$SUBSYSTEM_DIR/config:ipc_util_config",
    ":libipc_core_private_config",
  ]
  public_configs = [
    "$SUBSYSTEM_DIR/config:dsoftbus_header_deps_config",
    "$SUBSYSTEM_DIR/config:ipc_util_config",
    ":ipc_all_deps_config",
  ]

  all_dependent_configs = [ ":ipc_all_deps_config" ]

  deps = [ "$IPC_CORE_ROOT/src/core:ipc_common" ]

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "c_utils:utils",
    "ffrt:libffrt",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "hitrace:libhitracechain",
    "selinux:libselinux",
  ]
  if (!defined(ohos_lite)) {
    external_deps += [ "faultloggerd:libbacktrace_local" ]
  }
  defines = [ "FFRT_IPC_ENABLE" ]
  if (build_selinux) {
    defines += [ "WITH_SELINUX" ]
  }
  innerapi_tags = [
    "platformsdk",
    "sasdk",
  ]

  install_images = [ system_base_dir ]
  subsystem_name = "communication"
  part_name = "ipc"
}
